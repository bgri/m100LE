10 REM
11 REM
12 REM
13 REM
14 REM
15 REM
16 AD$=DATE$:MD=0
17 IF MD=1 THEN GOSUB 8150
20 DB=0:VR$="v0.l"
21 IF RF=1 THEN AD$=DATE$
23 CU=41:CO=110
24 GU$="_____":BG$=".....":GW$=BG$
25 DL=400
29 G1=41:W1=G1+7
30 G2=81:W2=G2+7
31 G3=121:W3=G3+7
32 G4=161:W4=G4+7
33 G5=201:W5=G5+7
34 G6=241:W6=G6+7
35 A1$="ABCDEFGHIJKLM": A2$="NOPQRSTUVWXYZ"
36 RV$=CHR$(27)+"p": NV$=CHR$(27)+"q"
37 DIM SO$(6):FOR I=1 TO 6:SO$(I)=BG$:NEXT I
40 REM
41 CLS
42 PRINT @ G1 ,GU$:PRINT @ W1,GW$
43 PRINT @ G2 ,GU$:PRINT @ W2,GW$
44 PRINT @ G3 ,GU$:PRINT @ W3,GW$
45 PRINT @ G4 ,GU$:PRINT @ W4,GW$
46 PRINT @ G5 ,GU$:PRINT @ W5,GW$
47 PRINT @ G6 ,GU$:PRINT @ W6,GW$
48 PRINT @ 16,"-m100le-":PRINT @ 214, A1$:PRINT @ 254, A2$
49 PRINT @ 68,"|":PRINT @ 108,"|":PRINT @ 148,"|":PRINT @ 188,"|":PRINT @ 228,"|":PRINT @ 268,"|"
50 ON ERROR GOTO 9000
60 PRINT CHR$(27) "P";
65 WF$="WL20XX.DO"
68 DIM WD$(5)
69 DIM DA(12)
70 REM
73 IF RF<>1 THEN DY=0
77 GN=1
78 TW$="":TM$=""
79 N=0:I=0:R=0:C=0
80 REM
82 DATA 31,28,31,30,31,30,31,31,30,31,30,31
83 FOR I = 1 TO 12: READ DA(I):NEXT I
84 GOSUB 8300
90 REM
100 REM
299 REM
300 REM
400 REM
410 GOSUB 6000
500 REM
900 REM
910 GOSUB 7600
1000 REM
1010 GOSUB 7000
1015 REM
1020 GOSUB 7100
1999 GOTO 1000
2000 REM
3000 REM
3010 CO=110
3020 IF CC<=1 THEN PRINT @  CO,"HMMM...";: FOR NQ= 1 TO DL: NEXT NQ: PRINT @ CO, "          ": GOSUB 7600
3030 IF CC=2 THEN PRINT @  CO,"OK...";: FOR NQ= 1 TO DL: NEXT NQ: PRINT @ CO, "          ": GOSUB 7600
3040 IF CC=3 THEN PRINT @  CO,"NICE";: FOR NQ= 1 TO DL: NEXT NQ: PRINT @ CO, "          ": GOSUB 7600
3060 IF CC>=4 THEN PRINT @  CO,"WELL DONE";: FOR NQ= 1 TO DL: NEXT NQ: PRINT @ CO, "          ": GOSUB 7600
3070 RETURN
4000 REM
5000 REM
5010 REM
5020 CLS:CU=16:PRINT @ CU,"-m100le-":CU=CU-3
5025 IF RF=1 THEN PRINT @ 29,"RAND("+STR$(DY)+")" ELSE PRINT @ 40-LEN(WF$), WF$
5026 PRINT @ 80-LEN(AD$),AD$
5028 PRINT @ 120-LEN(VR$),VR$
5029 PRINT @ 160-LEN(STR$(DY)),DY
5030 FOR I = 1 TO 6: CU=CU+40: FOR J=1 TO 5
5033 IF ASC(MID$(SO$(I),J,1))>=65 THEN MID$(SO$(I),J,1)="*"
5035 NEXT J
5037 PRINT @CU,I;"- ";SO$(I): NEXT I
5040 CU=65:PRINT @ CU, RV$ GN;:PRINT @ CU+2, "/6 " NV$
5050 PRINT @41, "WORDLE FOR"
5060 PRINT @81, "m100"
5100 K$=INKEY$: IF K$="" THEN GOTO 5100
5500 CLS:CU=16:PRINT @ CU,"-m100le-"
5560 SV=1:GOTO 8910
6000 REM
6001 REM
6002 PRINT @ 72, RV$ "LOADING" NV$;
6010 OPEN WF$ FOR INPUT AS #1
6015 N=1
6020 LINE INPUT #1,TW$
6025 IF EOF(1) THEN GOTO 6050
6028 IF TW$ = "" THEN 6020
6030 IF N<> DY THEN: N=N+1: GOTO 6020
6040 REM
6050 CLOSE #1
6060 FOR I=1 TO 5: WD$(I) = MID$(TW$,I,1):NEXT I
6065 PRINT @ 72, "       ": PRINT @ 80-LEN(STR$(DY)),DY:IF DB=1 THEN TW$="HIPPY"
6070 RETURN
6999 GOTO 10000
7000 REM
7001 REM
7002 REM
7005 N=LEN(TM$)
7010 K$ = INKEY$: IF K$="" GOTO 7010
7015 X=ASC(K$)
7020 IF X=8 OR X=13 THEN :K$="":RETURN
7022 IF X<65 THEN K$=""
7025 IF X>90 AND X<97 THEN K$=""
7026 IF X>122 THEN K$=""
7040 IF K$="" THEN GOTO 7010
7050 IF ASC(K$)>=97 THEN K$=CHR$(X-32)
7060 RETURN
7070 REM
7100 REM
7105 IF X=8 THEN GOSUB 7200
7107 IF X=13 THEN GOTO 7300
7110 TM$=LEFT$(TM$,4)
7120 N=LEN(TM$)
7130 TM$=TM$+K$
7180 GOSUB 7600
7185 REM
7190 REM
7199 RETURN
7200 REM
7210 REM
7220 REM
7230 N=LEN(TM$)
7240 IF N>=1 THEN: TM$=LEFT$(TM$,N-1)
7250 IF N>0 THEN: PRINT @ CU+N-1,"_";
7260 GOSUB 7600
7280 RETURN
7300 REM
7302 REM
7310 REM
7315 IF LEN(TM$)< 5 THEN GOTO 7000
7325 REM
7330 GW$=BG$
7335 H$=TM$
7340 FOR I = 1 TO 5
7341 IF MID$(H$,I,1) = MID$(TW$,I,1) THEN MID$(GW$,I,1) = MID$(TW$,I,1)
7342 IF MID$(H$,I,1) = MID$(TW$,I,1) THEN MID$(H$,I,1) = "!" :CC=CC+1
7344 NEXT I
7346 FOR I = 1 TO 5
7348 IF MID$(H$,I,1)="!" THEN GOTO 7358
7350 FOR J = 1 TO 5
7351 IF MID$(H$,J,1)="!" THEN GOTO 7356
7353 IF MID$(H$,J,1) = MID$(TW$,I,1) THEN MID$(GW$,J,1) = "?":CC=CC+1
7356 NEXT J
7358 NEXT I
7360 IF GN<=1 THEN: PRINT @ W1, GW$;: SO$(1)=GW$: CU=G2
7362 IF GN=2 THEN: PRINT @ W2, GW$;: SO$(2)=GW$: CU=G3
7364 IF GN=3 THEN: PRINT @ W3, GW$;: SO$(3)=GW$: CU=G4
7366 IF GN=4 THEN: PRINT @ W4, GW$;: SO$(4)=GW$: CU=G5
7368 IF GN=5 THEN: PRINT @ W5, GW$;: SO$(5)=GW$: CU=G6
7370 IF GN=6 THEN: PRINT @ W6, GW$;: SO$(6)=GW$
7375 IF TM$=TW$ THEN WN=1: PRINT @ 135, RV$ " CONGRATS! " NV$;: FOR I = 1 TO DL: NEXT I: GOTO 8900
7380 GOSUB 7400
7382 GOSUB 3000:CC=1
7384 IF WN=1 THEN GOTO 8900
7394 GW$=BG$
7395 IF GN=6 THEN GOTO 8900
7399 GN=GN+1: TM$="":GOSUB 7600:GOTO 7190
7400 REM
7405 REM
7410 FOR I = 1 TO 5
7411 T=ASC(MID$(TM$,I,1)):T=T-64
7415 IF T<=13 THEN IF MID$(A1$,T,1) <> "*" THEN: IF MID$(TM$,I,1) = MID$(GW$,I,1) THEN MID$(A1$,T,1)="*" ELSE MID$(A1$,T,1)=MID$(GW$,I,1)
7420 IF T>13 THEN IF MID$(A2$,T-13,1) <> "*" THEN: IF MID$(TM$,I,1) = MID$(GW$,I,1) THEN MID$(A2$,T-13,1) = "*" ELSE MID$(A2$,T-13,1) = MID$(GW$,I,1)
7460 REM
7480 NEXT I
7485 GOSUB 8800
7490 PRINT @ 214, A1$
7495 PRINT @ 254, A2$
7499 RETURN
7500 REM
7510 REM
7570 REM
7580 REM
7600 REM
7640 PRINT @ CU,"_____";
7650 PRINT @ CU,TM$;
7660 RETURN
7999 GOTO 10000
8000 REM
8150 REM
8160 IF MD<>1 THEN RETURN
8170 CLS: PRINT @ 8, "DATE (MM/DD/YY):";
8180 LINE INPUT "";AD$
8185 IF AD$ = "" THEN AD$=DATE$
8190 RETURN
8300 REM
8301 REM
8301 REM
8310 REM
8320 CM=VAL(LEFT$(AD$,2))
8322 REM
8325 REM
8335 IF RF<>1 AND CM>1 THEN: FOR I = 1 TO CM-1: DY=DY+DA(I): NEXT I
8337 IF RF<>1 THEN: DY=DY+VAL(MID$(AD$,4,2))
8338 Y=2000+VAL(MID$(AD$,7,2))
8339 IF CM>2 AND ( (Y MOD 4 = 0) AND ( (Y MOD 100 <> 0) OR (Y MOD 400 = 0) ) ) THEN DY=DY+1
8340 REM
8350 MID$(WF$,5) = RIGHT$(AD$,2)
8360 REM
8365 IF RF=1 THEN PRINT @ 36,"RAND" ELSE PRINT @ 40-LEN(WF$), WF$
8399 RETURN
8800 REM
8860 RETURN
8900 REM
8903 PRINT CHR$(27) "Q"
8905 IF GN>=6 AND WN<>1 THEN PRINT @136, RV$ " SORRY! " NV$
8910 IF SV=1 THEN CO=96
8912 PRINT @ CO,"[A]GAIN?":PRINT @ CO+40,"[R]ANDOM?":PRINT @ CO+80,"[S]OCIAL?":PRINT @ CO+120,"[Q]UIT?";
8915 REM
8920 K$ = INKEY$: IF K$="" GOTO 8920
8925 IF K$="a" OR K$="A" THEN CLEAR:CLS: GOTO 10
8930 IF K$="r" OR K$="R" THEN CLEAR:CLS: RF=1:RT=VAL(RIGHT$(TIME$,2)): FOR I=1 TO RT:DY=FIX(RND(RT)*365):NEXT I:MD=0:GOTO 20
8935 IF K$="s" OR K$="S" THEN GOTO 5000
8938 IF K$="q" OR K$="Q" THEN MENU
8940 PRINT @ CO-2,"        ":PRINT @ CO+40,"         ":PRINT @ CO+80,"      ":PRINT @ CO, "ENDING...";:FOR I = 1 TO DL: NEXT I: CLS: END
9000 REM
9005 REM
9010 IF ERR=52 THEN CLS: PRINT "PROGAM STOP": PRINT "DATA FILE NOT FOUND (";WF$")": END
9200 PRINT @ 70, RV$ ERR;"-";ERL NV$:END
9999 GOTO 9999
10000 PRINT "ERROR - YOU SHOULD NOT HAVE GOTTEN SO FAR":STOP
