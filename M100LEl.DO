0 CLEAR
10 'WORDLE FOR THE TANDY M100
11 'ADAPTED BY BRAD GRIER
12 'BLOG.BRADGRIER.COM
13 'UNCOMMENTED VERSION AND SUPPORTING FILES
14 'AT : https://github.com/bgri/m100LE
15 '
16 ' MD=1 ' SET MD=1 TO MANUALLY ENTER DATE, OTHERWISE 0 TO SKIP
17 GOSUB 8150: 'SET AD$ (ALTDATE) TO DATE$ IF MD=0. ASK FOR DATE INPUT IF MD=1.
20 DB=0:VR$="v0.l": 'V UPDATE - add manual date entry
21 IF RF=1 THEN AD$=DATE$
23 CU=41:CO=110: 'COMMENTS
24 GU$="_____":BG$=".....":GW$=BG$
25 DL=400: 'DELAY'
29 G1=41:W1=G1+7: 'ROW 1
30 G2=81:W2=G2+7: 'ROW 2
31 G3=121:W3=G3+7: 'ROW 3
32 G4=161:W4=G4+7: 'ROW 4
33 G5=201:W5=G5+7: 'ROW 5
34 G6=241:W6=G6+7: 'ROW 6
35 A1$="ABCDEFGHIJKLM": A2$="NOPQRSTUVWXYZ": 'ALPHABET BOARD
36 RV$=CHR$(27)+"p": NV$=CHR$(27)+"q": 'REVERSE / NORMAL VIDEO
37 DIM SO$(6):FOR I=1 TO 6:SO$(I)=BG$:NEXT I
40 'SET UP SCREEN
41 CLS: ' WORD AND RESULT BOXES
42 PRINT @ G1 ,GU$:PRINT @ W1,GW$
43 PRINT @ G2 ,GU$:PRINT @ W2,GW$
44 PRINT @ G3 ,GU$:PRINT @ W3,GW$
45 PRINT @ G4 ,GU$:PRINT @ W4,GW$
46 PRINT @ G5 ,GU$:PRINT @ W5,GW$
47 PRINT @ G6 ,GU$:PRINT @ W6,GW$
48 PRINT @ 16,"-m100le-":PRINT @ 214, A1$:PRINT @ 254, A2$
49 PRINT @ 68,"|":PRINT @ 108,"|":PRINT @ 148,"|":PRINT @ 188,"|":PRINT @ 228,"|":PRINT @ 268,"|"
50 ON ERROR GOTO 9000: 'ERROR HANDLING'
60 PRINT CHR$(27) "P";: 'MAKE CURSOR VISIBLE 
65 WF$="WL20XX.CO": 'FILE CONTAINING THE YEAR'S WORDS. XX TO BE REPLACED BY CURRENT YY
68 DIM WD$(5): 'TODAY'S WORD
69 DIM DA(12): 'DAYS IN A MONTH ARRAY
70 '
73 IF RF<>1 THEN DY=0: 'DAYS FROM THE START OF THE YEAR'
77 GN=1: 'GUESS ATTEMPT NUMBER
78 TW$="":TM$="": 'TODAY'S WORD/TEMP WORD
79 N=0:I=0:R=0:C=0: 'LOCAL VARIABLES
80 'LOAD NUMBER OF DAYS FOR EACH MONTH INTO THE ARRAY DA()
82 DATA 31,28,31,30,31,30,31,31,30,31,30,31: 'DAYS IN EACH MONTH
83 FOR I = 1 TO 12: READ DA(I):NEXT I: 'POPULATE ARRAY'
84 GOSUB 8300: 'LOAD THE DATE/DAY/NUMBER & YEAR FOR WORDFILE (WF$)
90 ' RESERVED LINE NUMBERS FOR FUTURE FEATURES
100 '
299 '
300 'LOAD PLAYER STATS
400 'LOAD WORD LIST
410 GOSUB 6000: 'LOAD WORD FILE AND GET TODAY'S WORD (TW$)
500 '
900 'POSITION CURSOR BEFORE MAIN LOOP
910 GOSUB 7600
1000 'MAIN LOOP
1010 GOSUB 7000: 'GET KEYBOARD INPUT
1015 '
1020 GOSUB 7100: 'PROCESS KEYBOARD INPUT
1999 GOTO 1000
2000 REM RNDACC subroutine
2001 REM Input: WL$ is file to locate.
2002 REM Output: WA is address in RAM.
2003 REM Temp: ID, RD, FL, FN$, T1, T2
2004 REM
2005 REM Warning: Run CLEAR at start of program or this will return an invalid address.
2006 REM
2007 ' Normalize WL$ to 8 chars, no dot
2008 GOSUB 2100
2009 ' HW ID 51=M100, 171=T200, 148=NEC,            167=T102,  35=M10,  225=K85
2010 ID=PEEK(1)
2014 ' Ram Directory address. (Anderson's "Programming Tips" gives RD=63842 for M100/102 and 62034 for T200.)
2015 ' (Gary Weber's NEC.MAP gives RD=63567, but we can skip the system files by starting at 63633.)
2016 RD=-( 63842*(ID=51 OR ID=167) + 62034*(ID=171) + 63633*(ID=148) )
2017 IF RD=0 THEN PRINT "Error: Unknown machine ID";ID;". Please file a bug report.": END
2019 ' Search directory for WL$
2020 FOR T1 = RD TO 65535 STEP 11
2029 ' Attribute flag: See Oppedahl's "Inside the TRS-80 Model 100" for details.
2030 FL=PEEK(T1)
2039 ' Stop at end of directory (255)
2040 IF FL=255 THEN GOTO 2080
2044 ' Skip invalid files
2045 IF (FL AND 128)=0 THEN NEXT T1
2049 ' WA is file address in memory
2050 WA=PEEK(T1+1)+256*PEEK(T1+2)
2059 ' Filename matches WL$?
2060 FOR T2=1 TO 8: IF ASC(MID$(WL$,T2, 1)) <> PEEK(T1+2+T2) THEN NEXT T1: ELSE NEXT T2
2070 IF T2=9 THEN RETURN: ' Found at address WA
2080 REM File not found
2085 ERROR 52: ' Raise FILE NOT FOUND
2090 WA=0: RETURN
2100 REM Normalize filename to 8 chars
2101 REM E.g. "FOO.DO" -> "FOO   DO"
2102 REM INPUT & OUTPUT: WL$
2103 REM Temp: T1, T2, FN$, EX$
2110 T1=INSTR(1,WL$,".")
2115 FN$=WL$:EX$=""
2120 IF T1>0 THEN FN$=MID$(WL$,1,T1-1): EX$=MID$(WL$,T1+1,2)
2130 IF LEN(FN$)>6 THEN PRINT "filename too long": STOP
2140 IF LEN(FN$)<6 THEN FN$=FN$+" ": GOTO 2140
2150 IF LEN(EX$)<2 THEN EX$=EX$+" ": GOTO 2150
2160 FN$=FN$+EX$: WL$=""
2170 FOR T1=1 TO 8
2172 T2=ASC(MID$(FN$,T1,1)): IF (T2>=ASC("a")) AND (T2<=ASC("z")) THEN T2=T2-32
2173 WL$=WL$+CHR$(T2)
2175 NEXT T1
2180 RETURN
3000 'WITTY COMMENTS
3010 CO=110: ' COMMENT DISPLAY LOCATION
3020 IF CC<=1 THEN PRINT @  CO,"HMMM...";: FOR NQ= 1 TO DL: NEXT NQ: PRINT @ CO, "          ": GOSUB 7600
3030 IF CC=2 THEN PRINT @  CO,"OK...";: FOR NQ= 1 TO DL: NEXT NQ: PRINT @ CO, "          ": GOSUB 7600
3040 IF CC=3 THEN PRINT @  CO,"NICE";: FOR NQ= 1 TO DL: NEXT NQ: PRINT @ CO, "          ": GOSUB 7600
3060 IF CC>=4 THEN PRINT @  CO,"WELL DONE";: FOR NQ= 1 TO DL: NEXT NQ: PRINT @ CO, "          ": GOSUB 7600
3070 RETURN
4000 '
5000 'SOCIAL
5010 '
5020 CLS:CU=16:PRINT @ CU,"-m100le-":CU=CU-3
5025 IF RF=1 THEN PRINT @ 29,"RAND("+STR$(DY)+")" ELSE PRINT @ 40-LEN(WF$), WF$
5026 PRINT @ 80-LEN(AD$),AD$
5028 PRINT @ 120-LEN(VR$),VR$
5029 PRINT @ 160-LEN(STR$(DY)),DY
5030 FOR I = 1 TO 6: CU=CU+40: FOR J=1 TO 5
5033 IF ASC(MID$(SO$(I),J,1))>=65 THEN MID$(SO$(I),J,1)="*"
5035 NEXT J
5037 PRINT @CU,I;"- ";SO$(I): NEXT I
5040 CU=65:PRINT @ CU, RV$ GN;:PRINT @ CU+2, "/6 " NV$
5050 PRINT @41, "WORDLE FOR"
5060 PRINT @81, "m100"
5100 K$=INKEY$: IF K$="" THEN GOTO 5100
5500 CLS:CU=16:PRINT @ CU,"-m100le-"
5560 SV=1:GOTO 8910
6000 '
6001 'LOAD WORD OF THE DAY
6002 PRINT @ 72, RV$ "LOADING" NV$;
6005 WL$=WF$
6009 REM Set WA to RAM addr of file
6010 GOSUB 2000
6019 REM Set TW$ to today's word from either compressed .CO or ASCII .DO file.
6020 IF RIGHT$(WF$,2)="CO" THEN GOSUB 6100 ELSE GOSUB 6200
6060 FOR I=1 TO 5: WD$(I) = MID$(TW$,I,1):NEXT I: 'COPY WORD INTO ARRAY'
6065 PRINT @ 72, "       ": PRINT @ 80-LEN(STR$(DY)),DY:IF DB=1 THEN TW$="HIPPY": 'CLEAR LOADING TEXT, PRINT WORD SEQ. NUM, OVERRIDE CURRENT WORD IF DB=1(DEBUG ON)'
6070 RETURN
6100 REM READ WORD FROM BINARY .CO FILE
6101 ' INPUT: WA is address of file,                  DY is day of the year
6102 ' OUTPUT: TW$ is today's word
6103 ' TEMP: A, B, X, I
6110 X=WA+6+(DY-1)*3
6129 ' In: Three Base-256 digits
6130 A=PEEK(X)+256*PEEK(X+1)+256*256*PEEK(X+2)
6139 ' Out: Five Base-26 letters
6140 FOR I=1 TO 5
6150 B=INT(A/26)
6160 TW$=TW$+CHR$(A-B*26+ASC("A"))
6170 A=B
6180 NEXT I
6190 RETURN
6200 REM READ WORD FROM ASCII .DO FILE
6201 ' INPUT: WA is address of file,                   DY is day of the year
6202 ' OUTPUT: TW$ is today's word
6203 ' TEMP: A, B, X, I
6209 ' 7 bytes per word (5 chars + CRLF)
6210 X=WA+(DY-1)*7
6229 ' Read five ASCII chars
6230 FOR I=0 TO 4
6240 A=(PEEK(X+I) AND 95) ' CAPITALIZE
6250 TW$=TW$+CHR$(A)
6270 NEXT I
6290 RETURN
6999 GOTO 10000: 'WENT TOO FAR -- abort, abort.
7000 '
7001 'GET KEYBOARD INPUT
7002 '
7005 N=LEN(TM$)
7010 K$ = INKEY$: IF K$="" GOTO 7010
7015 X=ASC(K$)
7020 IF X=8 OR X=13 OR X=21 THEN :K$="":RETURN: 'BKSP, ENTER, ^U. EXIT. NO MORE PROCESSING NEEDED'
7022 IF X<65 THEN K$="": 'FILTER FOR NON-LETTERS
7025 IF X>90 AND X<97 THEN K$=""
7026 IF X>122 THEN K$=""
7040 IF K$="" THEN GOTO 7010
7050 IF ASC(K$)>=97 THEN K$=CHR$(X-32): 'CONVERT LC TO UC
7060 RETURN
7070 '
7100 ' PROCESS KEYBOARD INPUT (K$ AND X)
7105 IF X=8 THEN GOSUB 7200: ' BACKSPACE
7107 IF X=13 THEN GOTO 7300: ' RETURN/ENTER
7108 IF X=21 THEN TM$="": GOSUB 7200: ' CTRL-U CLEAR INPUT
7110 TM$=LEFT$(TM$,4): 'TRIM TO 4 CHARACTERS'
7120 N=LEN(TM$): 'ACCEPT THE LAST TYPED CHARACTER'
7130 TM$=TM$+K$: 'POPULATE TEMPWORD'
7180 GOSUB 7600
7185 '
7190 'RETURN FROM ENTER CHECK AND UPDATE' DON'T DELETE ME
7199 RETURN
7200 'PROCESS BACKSPACE'
7210 '
7220 '
7230 N=LEN(TM$)
7240 IF N>=1 THEN: TM$=LEFT$(TM$,N-1): 'BACKSPACE
7250 IF N>0 THEN: PRINT @ CU+N-1,"_";: ' ERASE LETTER
7260 GOSUB 7600: 'REDRAW WORD, POSITION CURSOR
7280 RETURN
7300 'PROCESS ENTER/RETURN.THIS CAME FROM A GOTO IN LINE 7107
7302 'IF WORD MATCHES GOTO WIN ELSE UPDATE HINTS AND ALPHABET SCREEN
7310 '
7315 IF LEN(TM$)< 5 THEN GOTO 7000: 'WORD IS TOO SHORT, GET OUT!'
7325 ''
7330 GW$=BG$: 'RESET GUESSWORD$ TO .....'
7335 H$=TM$: '==== PROCESS THE GUESS ===='
7340 FOR I = 1 TO 5: 'TEST CHARACTER EXACT MATCH
7341 IF MID$(H$,I,1) = MID$(TW$,I,1) THEN MID$(GW$,I,1) = MID$(TW$,I,1)
7342 IF MID$(H$,I,1) = MID$(TW$,I,1) THEN MID$(H$,I,1) = "!" :CC=CC+1
7344 NEXT I
7346 FOR I = 1 TO 5: 'TEST CHARACTER SOURCE - LETTERS 1 - 5 W1-W5
7348 IF MID$(H$,I,1)="!" THEN GOTO 7358: ' IF TEST CHARACTER HAS BEEN FOUND THEN MOVE ON TO NEXT
7350 FOR J = 1 TO 5: 'GUESS TEST - COMPARE G1 VS W1, G2 VS W1...
7351 IF MID$(H$,J,1)="!" THEN GOTO 7356: ' IF FOUND FLAG FOUND, NEXT J
7353 IF MID$(H$,J,1) = MID$(TW$,I,1) THEN MID$(GW$,J,1) = "?":CC=CC+1: 'MARK THE GUESS AND INCREMENT COMMENT VALUE'
7356 NEXT J
7358 NEXT I
7360 IF GN<=1 THEN: PRINT @ W1, GW$;: SO$(1)=GW$: CU=G2
7362 IF GN=2 THEN: PRINT @ W2, GW$;: SO$(2)=GW$: CU=G3
7364 IF GN=3 THEN: PRINT @ W3, GW$;: SO$(3)=GW$: CU=G4
7366 IF GN=4 THEN: PRINT @ W4, GW$;: SO$(4)=GW$: CU=G5
7368 IF GN=5 THEN: PRINT @ W5, GW$;: SO$(5)=GW$: CU=G6
7370 IF GN=6 THEN: PRINT @ W6, GW$;: SO$(6)=GW$
7375 IF TM$=TW$ THEN WN=1: PRINT @ 135, RV$ " CONGRATS! " NV$;: FOR I = 1 TO DL: NEXT I: GOTO 8900: 'WIN. SET WIN FLAGS'
7380 GOSUB 7400: 'UPDATE ALPHABET BOARD'
7382 GOSUB 3000:CC=1: 'GET COMMENTS AND RESET COMMENT INCREMENT
7384 IF WN=1 THEN GOTO 8900: 'GO TO WIN SCREEN'
7394 GW$=BG$: 'TM$="": '
7395 IF GN=6 THEN GOTO 8900
7399 GN=GN+1: TM$="":GOSUB 7600:GOTO 7190: 'RESET AND GET NEXT GUESS'
7400 ' UPDATE ALPHABET BOARD
7405 ' GW$ IS THE CURRENT CLUE
7410 FOR I = 1 TO 5
7411 T=ASC(MID$(TM$,I,1)):T=T-64
7415 IF T<=13 THEN IF MID$(A1$,T,1) <> "*" THEN: IF MID$(TM$,I,1) = MID$(GW$,I,1) THEN MID$(A1$,T,1)="*" ELSE MID$(A1$,T,1)=MID$(GW$,I,1)
7420 IF T>13 THEN IF MID$(A2$,T-13,1) <> "*" THEN: IF MID$(TM$,I,1) = MID$(GW$,I,1) THEN MID$(A2$,T-13,1) = "*" ELSE MID$(A2$,T-13,1) = MID$(GW$,I,1)
7460 ''
7480 NEXT I
7485 GOSUB 8800: 'STOP''
7490 PRINT @ 214, A1$
7495 PRINT @ 254, A2$
7499 RETURN
7500 'PRINT GUESS RESULTS: GOSUB 7400
7510 '
7570 'GOSUB 8200
7580 'RETURN
7600 'REDRAW WORD, POSITION CURSOR
7640 PRINT @ CU,"_____";
7650 PRINT @ CU,TM$;
7660 RETURN
7999 GOTO 10000
8000 'SMALL ROUTINES
8150 'MANUAL DATE ENTRY. MD=1 TO ENABLE MANUAL DATE ENTRY. AD$=ALTERNATE DATE
8160 IF MD<>1 THEN GOTO 8185
8170 CLS
8174 PRINT "Input date as MM/DD/YY, or DAY#/YY, or"
8175 PRINT "hit ENTER for ";DATE$
8176 PRINT
8178 PRINT "        DATE: ";
8180 LINE INPUT "";AD$
8185 IF AD$ = "" THEN AD$=DATE$
8190 RETURN
8300 'CALCULATE DAY (#) OF THE YEAR
8305 ' If user inputs a day number without a year (e.g., "213"), presume current year.
8306 IF INSTR(1, AD$, "/")=0 THEN AD$=AD$+RIGHT$(DATE$,3)
8307 ' If user inputs "DY/YR" (e.g., "213/22"), then use DY as ordinal date.
8308 IF MID$(AD$,6, 1) <> "/" THEN DY=VAL(AD$): GOTO 8337
8310 '
8320 CM=VAL(LEFT$(AD$,2)): 'GET NUMBER OF THE CURRENT MONTH'
8322 'PRINT CM
8325 'LOOP MONTH # OF TIMES - 1: 'ADD DAYS FROM EACH MONTH PREVIOUS: 'ADD DAYS OF CURRENT MONTH
8335 IF RF<>1 AND CM>1 THEN: FOR I = 1 TO CM-1: DY=DY+DA(I): NEXT I
8336 IF RF<>1 THEN: DY=DY+VAL(MID$(AD$,4,2))
8337 Y=2000+VAL(RIGHT$(AD$,2)): 'FOUR DIGIT YEAR
8338 LP=-( (Y MOD 4 = 0) AND ( (Y MOD 100 <> 0) OR (Y MOD 400 = 0) ) ): ' LP is 1 in leap years, 0 otherwise.
8339 IF CM>2 THEN DY=DY+LP
8340 'PRINT "#DAYS=";DY: 'PRINT NUMBER OF DAYS IN YEAR TO TODAY - THIS IS THE NUMBER FOR TODAY'S WORD
8345 IF DY<0 OR DY>365+LP THEN ERROR 6: ' Overflow error if DY is not in the calendar.
8350 MID$(WF$,5) = RIGHT$(AD$,2): 'CREATE THE YEAR'S WORD FILE NAME FROM AD$
8360 '
8365 IF RF=1 THEN PRINT @ 36,"RAND" ELSE PRINT @ 40-LEN(WF$), WF$
8399 RETURN
8800 'DEBUG ROUTINES
8860 RETURN
8900 'WIN OR FAIL AFTER 6 GUESSES
8903 PRINT CHR$(27) "Q": 'HIDE CURSOR
8905 IF GN>=6 AND WN<>1 THEN PRINT @136, RV$ " SORRY! " NV$: 'CANADIAN 'EH?'
8910 IF SV=1 THEN CO=96
8912 PRINT @ CO,"[A]GAIN?":PRINT @ CO+40,"[R]ANDOM?":PRINT @ CO+80,"[S]OCIAL?":PRINT @ CO+120,"[Q]UIT?";
8915 ''
8920 K$ = INKEY$: IF K$="" GOTO 8920
8925 IF K$="a" OR K$="A" THEN CLEAR:CLS:MD=1: GOTO 10: 'COMPLETE RESTART, ASK FOR DATE
8930 IF K$="r" OR K$="R" THEN CLEAR:CLS: RF=1:RT=VAL(RIGHT$(TIME$,2)): FOR I=1 TO RT:DY=FIX(RND(RT)*365):NEXT I:MD=0:GOTO 20
8935 IF K$="s" OR K$="S" THEN GOTO 5000: 'GOTO SOCIAL THEN END
8938 IF K$="q" OR K$="Q" THEN MENU
8940 PRINT @ CO-2,"        ":PRINT @ CO+40,"         ":PRINT @ CO+80,"      ":PRINT @ CO, "ENDING...";:FOR I = 1 TO DL: NEXT I: CLS: END
9000 'ERROR HANDLING'
9005 'FILE NOT FOUND
9010 IF ERR=52 THEN CLS: PRINT "PROGAM STOP": PRINT "DATA FILE NOT FOUND (";WF$")": END
9020 IF ERR=6 THEN CLS: PRINT"PROGRAM STOP": PRINT "DATE OUT OF RANGE (";AD$")": END
9200 PRINT @ 70, RV$ ERR;"-";ERL NV$
9210 PRINT"Error"; ERR ;"in line"; ERL
9230 ERROR ERR
9290 END
9999 GOTO 9999
10000 PRINT "ERROR - YOU SHOULD NOT HAVE GOTTEN SO FAR":STOP
